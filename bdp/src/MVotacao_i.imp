/* MVotacao_i
 * Author: Josenildo Vicente
 * Creation date: 28/04/2021
 */

IMPLEMENTATION MVotacao_i
REFINES MVotacao

SEES
    MVotacao_Ctx


CONCRETE_VARIABLES
    estado_i
    , eleitores_i
    , candidatos_i
    , local_i
    , votos_i
    , registro_i

INVARIANT
    estado_i : ESTADO
    & eleitores_i : CPFs --> BOOL
    & candidatos_i : CPFs --> BOOL
    & local_i : CPFs --> ( ZONAS --> BOOL )
    & votos_i : CPFs --> NAT
    & registro_i : CPFs --> BOOL

INITIALISATION
    estado_i := preparacao ;
    eleitores_i := CPFs * { FALSE } ;
    candidatos_i := CPFs * { FALSE } ;
    local_i := CPFs * { ( ZONAS * { FALSE } ) } ;
    votos_i := CPFs * { 0 } ;
    registro_i := CPFs * { FALSE }


OPERATIONS
    adicionar_eleitor ( cpf , zona ) =
    BEGIN
        VAR ii IN
            ii := eleitores_i ( cpf ) ;
            IF ii = FALSE
            THEN
                eleitores_i ( cpf ) := TRUE ;
                local_i ( cpf ) ( zona ) := TRUE ;
                registro_i ( cpf ) := FALSE
            END
        END
    END
    ;


    remover_eleitor ( cpf ) =
    BEGIN
        VAR ii , oo IN
            oo := eleitores_i ( cpf ) ;
            ii := candidatos_i ( cpf ) ;
            IF oo = TRUE
            THEN
                IF ii = TRUE
                THEN
                    votos_i ( cpf ) := 0 ;
                    candidatos_i ( cpf ) := FALSE
                END ;
                local_i ( cpf ) := ( ZONAS * { FALSE } ) ;
                registro_i ( cpf ) := FALSE ;
                eleitores_i ( cpf ) := FALSE
            END
        END
    END
    ;


    mudar_local ( cpf , zona ) =
    BEGIN
        VAR ii IN
            ii := eleitores_i ( cpf ) ;
            IF ( ii = TRUE )
            THEN
                local_i ( cpf ) := ZONAS * { FALSE } ;
                local_i ( cpf ) ( zona ) := TRUE
            END
        END

    END
    ;


    adicionar_candidato ( cpf ) =
    BEGIN
        VAR ii , oo IN
            ii := eleitores_i ( cpf ) ;
            oo := candidatos_i ( cpf ) ;
            IF ( ii = TRUE ) & ( oo = FALSE )
            THEN
                candidatos_i ( cpf ) := TRUE ;
                votos_i ( cpf ) := 0
            END
        END
    END
    ;


    remover_candidato ( cpf ) =
    BEGIN
        VAR ii , oo IN
            ii := eleitores_i ( cpf ) ;
            oo := candidatos_i ( cpf ) ;
            IF ( ii = TRUE ) & ( oo = TRUE )
            THEN
                votos_i ( cpf ) := 0 ;
                candidatos_i ( cpf ) := FALSE
            END
        END
    END
    ;


    iniciar_votacao =
    BEGIN
        VAR eleitor_1 , eleitor_2 , eleitor_3 IN
            eleitor_1 := candidatos_i ( cpf1 ) ;
            eleitor_2 := candidatos_i ( cpf2 ) ;
            eleitor_3 := candidatos_i ( cpf3 ) ;
            IF ( eleitor_1 = TRUE ) or ( eleitor_2 = TRUE ) or ( eleitor_3 = TRUE )
            THEN
                estado_i := votacao
            END
        END

    END
    ;


    votar ( cpf , zona , cpf_candidato ) =
    BEGIN
        VAR ee , ii , oo , uu IN
            ii := eleitores_i ( cpf ) ;
            oo := candidatos_i ( cpf_candidato ) ;
            uu := registro_i ( cpf ) ;
            ee := local_i ( cpf ) ( zona ) ;
            IF ( ii = TRUE ) & ( ee = TRUE ) & ( uu = FALSE )
            THEN
                IF ( oo = TRUE )
                THEN
                    votos_i ( cpf_candidato ) := votos_i ( cpf_candidato ) + 1
                END ;
                registro_i ( cpf ) := TRUE
            END
        END
    END
    ;


    concluir_votacao =
    BEGIN
        IF ( estado_i = votacao )
        THEN
            estado_i := apuracao
        END
    END
    ;


    resultado <-- apurar_vencedor =
    BEGIN
        VAR cand_1 , cand_2 , cand_3 IN
            cand_1 := votos_i ( cpf1 ) ;
            cand_2 := votos_i ( cpf2 ) ;
            cand_3 := votos_i ( cpf3 ) ;

            IF ( cand_1 > cand_2 ) & ( cand_1 > cand_3 )
            THEN
                resultado := cpf1
            ELSE IF ( cand_2 > cand_1 ) & ( cand_2 > cand_3 )
                THEN
                    resultado := cpf2
                ELSE IF ( cand_3 > cand_1 ) & ( cand_3 > cand_2 )
                        THEN
                            resultado := cpf3
                    ELSE
                        resultado := indefinido
                    END
                END
            END
        END
    END

 END


/*

                     Obrigações de provas não provadas automáticamente:

- mudar_local.1
    estado = preparacao & cpf : CPFs & cpf : eleitores & cpf : dom(local) & zona : ZONAS & zona /= local(cpf) & cpf /= indefinido & btrue & eleitores_i$1(cpf) = TRUE & btrue  => local_i$1 <+ {cpf |-> ZONAS * {FALSE}} <+ {cpf |-> ((local_i$1 <+ {cpf |-> ZONAS * {FALSE}})(cpf) <+ {zona |-> TRUE})} : CPFs --> (ZONAS --> BOOL)
justificativa: 
    Acredito que ele não consegue linkar o que acontece na operação na maquina abstrata com o que acontece aqui na implementação. Nessa operação esta sendo setados todas as zonas do eleitor como False para depois colocar a zone que quer como True, com isso acaba encadeando a sobrescrita.

- votar.1
    estado = votacao & cpf : CPFs & cpf : eleitores & cpf /= indefinido & zona : ZONAS & cpf : dom(local) & cpf : dom(registro) & zona = local(cpf) & cpf_candidato : CPFs & cpf_candidato : candidatos \/ {indefinido} & registro(cpf) = FALSE & btrue & eleitores_i$1(cpf) = TRUE & local_i$1(cpf)(zona) = TRUE & registro_i$1(cpf) = FALSE & candidatos_i$1(cpf_candidato) = TRUE & btrue & cpf_candidato : candidatos & cpf_candidato : dom(votos) & votos(cpf_candidato) + 1 : NAT => votos_i$1 <+ {cpf_candidato |-> votos_i$1(cpf_candidato) + 1} : CPFs --> NAT
justificativa: 
    Ele não consegue provar que a soma ( votos_i(cpf_candidato) +1 ) é um NAT, e a soma de um NAT com outro NAT resulta num NAT!

- votar.3
    estado = votacao & cpf : CPFs & cpf : eleitores & cpf /= indefinido & zona : ZONAS & cpf : dom(local) & cpf : dom(registro) & zona = local(cpf) & cpf_candidato : CPFs & cpf_candidato : candidatos \/ {indefinido} & registro(cpf) = FALSE & btrue & eleitores_i$1(cpf) = TRUE & local_i$1(cpf)(zona) = TRUE & registro_i$1(cpf) = FALSE & candidatos_i$1(cpf_candidato) = TRUE & btrue & not(cpf_candidato : candidatos & cpf_candidato : dom(votos) & votos(cpf_candidato) + 1 : NAT) => votos_i$1 <+ {cpf_candidato |-> votos_i$1(cpf_candidato) + 1} : CPFs --> NAT
justificativa: 
    Essa obrigação de prova é parecida com a de cima, porém ele quer que seja provado que não vai dar nenhum problema de restrição quando fazer a soma do voto sem ter sido passado no IF da operação da maquina abstrata, só que as condições na operação aqui na implementação já garantem que nao vai ser somado o voto quando não passa no IF da maquina abstrata pois as condições da maquina abstrata estão sendo feitas na operação da implementação.

- apurar_vencedor.1
    *muito grande pra colar a OP*
justificativa: 
    Ele não prova que cpf é um candidato, nem que cpf faz parte do domínio de votos e outras coisas. Acontece que votos_i é inicializado com valor 0 para todos os CPFs, logo se um CPF não for candidato ele terá valor 0, logo como o resultado é o que tiver mais votos ou indefinido, o CPF que não for candidato terá quantidade de votos 0 então ele nunca será escolhido para ser o resultado ( o ganhador da eleição), tendo isso, se CPF não for um eleitor e candidato (não estiver setado como TRUE nas variáveis) não será nunca o resultado da operação!

- apurar_vencedor.3
    *muito grande pra colar a OP*
justificativa: 
    A justificativa é a mesma de apurar_vencedor.1 pois é exatamente o mesmo caso!

- apurar_vencedor.5
    *muito grande pra colar a OP*
justificativa: 
    A justificativa é a mesma de apurar_vencedor.1 pois é exatamente o mesmo caso!
    
- apurar_vencedor.7
    *muito grande pra colar a OP*
justificativa: 
    A justificativa é a mesma de apurar_vencedor.1 pois é exatamente o mesmo caso!
    
    
*/
